#
# This is the "you hold ..." problem from the manual.
#
generate   20000
produce    25
vulnerable EW
dealer     west
predeal    south SAQ542, HKJ87, D32, CAK
#
# Exclude other opening bids by west (1D/H/S/NT)
#
west1n = shape(west, any 4333 + any 4432 + any 5332 - 5xxx - x5xx) &&
         hcp(west)>14 && hcp(west)<18
west1h = hearts(west)>= 5
west1s = spades(west)>= 5
west1d = diamonds(west)>clubs(west) || ((diamonds(west)==clubs(west))==4)
west1c = hcp(west)>10 && clubs(west)>=3 &&
         (not west1n) && (not west1h) && (not west1s) && (not west1d)
#
# 2D overcall from north
#
north2d = (hcp(north)>5 && hcp(north)<12) &&
          shape(north, xx6x + xx7x - any 4xxx - any 5xxx)
#
# West bids 1C, north overcalls 2D
#
condition  west1c && north2d
#
action     printall

