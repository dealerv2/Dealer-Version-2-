generate 2000
produce 10
seed 13
title " Checking how to nest ternary tests "
# In C and Perl you can nest the ternary almost indefinitely as in
# Answ = ( cond1 ) ? Var1 :
#        ( cond2 ) ? Var2 :
#         ....
#         ( condn ) ? Varn : DefaultVar
# In Dealer Even this simple statement gives the wrong result
# # Answ = ( cond1 ) ? Var1 :
#          ( cond2 ) ? Var2 : Default
# If cond1 is false it always produces Default as the value of Answ
# However you CAN do some Nesting.
# To find the maximum of 4 Values you can do: (Note this returns the value, but not the name of the longest suit)
ls =(  ( spades(west) >= hearts(west) ? spades(west) : hearts(west) ) >=    // if The len of longest Major >=
       ( (diamond(west) >= clubs(west))  ? diamond(west) : clubs(west)) ) ? //    The len of the longest Minor
       ( spades(west) >= hearts(west) ? spades(west) : hearts(west) ) :       // set ls to the len of longest Major
       ( (diamond(west) >= clubs(west) ) ? diamond(west) : clubs(west) )      // else set ls to len of longest Minor

# To find the minimum of 4 Values you can do: (Note this returns the value, but not the name of the shortest suit)
ss = (  ( spades(west) <= hearts(west) ? spades(west) : hearts(west) ) <=    // if The len of shortest Major <=
       ( (diamond(west) <= clubs(west))  ? diamond(west) : clubs(west)) ) ?  //    The len of the shortest Minor
       ( spades(west) <= hearts(west) ? spades(west) : hearts(west) ) :      // set ss to the len of shortest Major
       ( (diamond(west) <= clubs(west) ) ? diamond(west) : clubs(west) )     // else set ss to len of shortest Minor

# But it might be more clear to have intermediate variables
SMaj = (spades(west)  <= hearts(west)) ? spades(west)  : hearts(west)
LMaj = (spades(west)  >= hearts(west)) ? spades(west)  : hearts(west)
SMin = (diamond(west) <= clubs(west) ) ? diamond(west) : clubs(west)
LMin = (diamond(west) >= clubs(west) ) ? diamond(west) : clubs(west)

SS   = (SMaj <= SMin ) ? SMaj : SMin
LS   = (LMaj >= LMin ) ? LMaj : LMin

# find length of second longest suit, (but not the suit name)
MLS =( ( hearts(west) ) >=    // if spades is the longest suit, then MLS is the value of the next longest suit.
       ( (diamond(west) >= clubs(west))  ? diamond(west) : clubs(west)) ) ?
       ( hearts(west) ) :
       ( (diamond(west) >= clubs(west) ) ? diamond(west) : clubs(west) )
MLH =( ( spades(west) ) >=   // if hearts is the longest suit, then MLH is the value of the next longest suit.
       ( (diamond(west) >= clubs(west))  ? diamond(west) : clubs(west)) ) ?
       ( spades(west) ) :
       ( (diamond(west) >= clubs(west) ) ? diamond(west) : clubs(west) )

MLD =( (  clubs(west) ) >=  // if diamonds is the longest suit, then MLD is the value of the next longest suit.
       ( (spades(west)  >= hearts(west)) ? spades(west)  : hearts(west))) ?
       ( clubs(west)  ) :
       ( (spades(west)  >= hearts(west)) ? spades(west)  : hearts(west))

MLC =( (  diamond(west) ) >=  // if clubs is the longest suit, then MLC is the value of the next longest suit.
       ( (spades(west)  >= hearts(west)) ? spades(west)  : hearts(west))) ?
       ( diamonds(west)  ) :
       ( (spades(west)  >= hearts(west)) ? spades(west)  : hearts(west))

# Now we figure out which of MLS, MLH, MLD, or MLC to use.
L2S =  (spades(west)  == LS ) ? MLS : 0      // if spades is the longest suit, then L2S is the next longest else L2S is 0
L2D =  (diamond(west) == LS ) ? MLD : 0
L2H =  (hearts(west)  == LS ) ? MLH : 0
L2C =  (clubs(west)   == LS ) ? MLC : -1

# We now find the Maximum of the 4 values L2S, L2H, L2D, L2C -- using temporary variables
L2Maj = (L2S >= L2H) ? L2S : L2H
L2Min = (L2D >= L2C) ? L2D : L2C
L2 = (L2Maj > L2Min) ? L2Maj : L2Min


action
    printrpt (west ,  "SS=",SS,ss , " LS=", LS,ls , " L2=", L2, " MLC=", MLC, " MLD=",MLD," MLH=",MLH," MLS=",MLS ),
